plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id("com.gradle.plugin-publish") version "1.3.1"
    id "com.diffplug.spotless" version "8.0.0"
}

java {
    // 17 is lowest but we support and recommend 21
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17)) // Beispiel: Java 17
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation "com.squareup.okhttp3:okhttp:5.1.0"
    implementation "org.apache.commons:commons-lang3:3.18.0"
    testImplementation 'com.github.tomakehurst:wiremock-jre8-standalone:3.0.1'
    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.swagger:swagger-parser:1.0.75'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'

    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
}

spotless {
    java {
        target project.fileTree('src').include('**/*.java')

        googleJavaFormat("1.26.0").aosp().reorderImports(false)

        importOrder("java", "javax", "org", "com", "net", "io", "lombok")
        toggleOffOn()
        trimTrailingWhitespace()
        leadingTabsToSpaces()
        endWithNewline()
    }
}

def versionFile = file("version.properties")

// Lese die aktuelle Version aus der Datei
def getVersionFromFile = {
    if (!versionFile.exists()) {
        return "2.0.0"
    }
    def properties = new Properties()
    versionFile.withInputStream { properties.load(it) }
    return properties.getProperty("version", "2.0.0")
}

// Version hochzählen
def incrementVersion = { version ->
    def parts = version.tokenize('.')
    if (parts.size() < 3) return "2.0.0" // Falls defekt, Standard setzen
    def patch = parts[2].toInteger() + 1
    return "${parts[0]}.${parts[1]}.${patch}"
}

// Schreibe die neue Version zurück in die Datei
def setVersionToFile = { newVersion ->
    def properties = new Properties()
    properties.setProperty("version", newVersion)
    versionFile.withWriter { writer -> properties.store(writer, null) }
}

version = getVersionFromFile()
group = 'io.github.ludy87.swagger.v2'

gradlePlugin {
    website.set("https://github.com/Ludy87/swaggerhub-gradle-plugin")
    vcsUrl.set("https://github.com/Ludy87/swaggerhub-gradle-plugin")
    plugins {
        swaggerhub {
            id = "io.github.ludy87.swagger.v2"
            implementationClass = "io.github.ludy87.swagger.swaggerhub.v2.SwaggerHubPlugin"
            displayName = "SwaggerHub Gradle Plugin V2"
            description = "Gradle Plugin for SwaggerHub Version 2 - based on https://github.com/SmartBear/swaggerhub-gradle-plugin"
            tags.set(['swagger', 'swaggerhub', 'openapi', 'api'])
        }
    }
}

tasks.register("incrementVersion") {
    doLast {
        def newVersion = incrementVersion(version)
        setVersionToFile(newVersion)
        println "Neue Version: $newVersion"
    }
}

// Erhöhe die Version nach einem erfolgreichen Build
tasks.named("build") {
    finalizedBy("incrementVersion")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    dependsOn "spotlessApply"
}

tasks.wrapper {
    gradleVersion = "9.0.0"
    distributionType = Wrapper.DistributionType.ALL
}
